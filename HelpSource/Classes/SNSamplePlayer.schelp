TITLE:: SNSamplePlayer
summary:: A sample looper, allowing to loop samples in various ways.
categories:: SNSampler
related:: Classes/SNSampler, Classes/AbstractSNSampler, Classes/CVCenter, Overviews/JITLib

DESCRIPTION::
Within its built-in looping modes a SNSamplePlayer lets you loop an array of consecutive buffers in such a way that all buffers are spread in the stereo or multichannel panorama. The built-in looping modes basically provide different ways of sequencing - either as audio grains within a link::Classes/Pbind:: or non-granular within a link::Classes/Pmono:: or link::Classes/Ndef::. All these modes will create different output characteristics. However, to make playing most convenient and flexible, they all make use of the wonderful link::Overviews/JITLib:: library, that allows an extremely flexible way of combining different concepts of sequencing and signal processing.

CLASSMETHODS::

METHOD:: new
Create a new SNSampleLooper instance

ARGUMENT:: name
Optional - a link::Classes/Symbol:: or link::Classes/String::, denoting the name of the new instance. If not given this willdefault to code::\Looper::.

ARGUMENT:: bufLength
Optional - the maximum length of a single loop in seconds. If not given this will default to code::60::. Often you will want to set this to the length of the buffers stored with an link::Classes/SNSampler::.

ARGUMENT:: mode
Optional - a link::Classes/Symbol:: or link::Classes/String::, denoting the mode in which to play the looper. As of currently this may be one of the following:
DEFINITIONLIST::
## code::\grain:: (default) || loops will be created as link::Classes/Synth:: grains in parallel playing link::Classes/Pbind::s within a link::Classes/Ppar::
## code::\mono:: || loops will be created in a continously playing link::Classes/Pmono:: within a link::Classes/Ppar::
## code::\ndef:: || loops will be playing in an link::Classes/Ndef::, using a link::Classes/BufRd::. Looping will be handled by a link::Classes/Phasor::
::

ARGUMENT:: numOutChannels
Optional - a link::Classes/Integer::, denoting the number of output channels. The looped buffers will automatically be spread accross all channels. Spreading the output is handled either by a link::Classes/Splay:: (if code::numOutChannels:: is set to code::2::) or a link::Classes/SplayAz:: ugen (for code::numOutChannels:: set to a value greater than code::2::). If not set this defaults to code::2::.

ARGUMENT:: server
Optional - the link::Classes/Server:: on which to play the looper. If not set this will default to code::Server.default::.

returns:: an SNSamplePlayer instance

METHOD:: all

returns:: an link::Classes/Event::, holding all current SNSamplePlayer instances


INSTANCEMETHODS::

METHOD:: setupPlayer
Set up the SNSamplePlayer before it can actually be played: Add an link::Classes/Array:: of buffers and define a node for an over-all volume control - setting the volume will be handled in a filter synth (more precisely: a link::Reference/NodeProxy_roles##NodeProxy role:: added to the end of an link::Classes/Ndef::). If not given explicitely the volume control will be created with an ID code::1000::.

ARGUMENT:: bufferArray
An link::Classes/Array:: of buffers that will be looped in the instance. Often this will be identical with an array of buffers created when setting up an link::Classes/SNSampler::

ARGUMENT:: volumeControlNode
Optional - an link::Classes/Integer::, denoting the position of the volume control in the chain of synths and filters (defined in an link::Classes/Ndef:: or link::Classes/NodeProxy::).
NOTE:: This ID is not a regular node ID in the server tree!::

METHOD:: initDef
Initialize or switch the mode in which the player will play. Available modes will be the ones described in link::#*new::.

ARGUMENT:: argMode
A link::Classes/Symbol:: - either code::\grain::, code::\mono:: or code::\ndef::

ARGUMENT:: bufferArray
Optional - an array of buffers, initialized with link::Classes/Buffer#*allocConsecutive::. NOTE::The number of buffers must be the same as the number of buffers for which the player was initialized!::

METHOD:: play
Start playing the SNSamplePlayer. Internally this will start playing the Ndef that keeps the loopers for all buffers. You may inspect the Ndef using an link::Classes/NdefMixer::.

METHOD:: pause
Pause the player. Internally this will pause the Ndef that keeps the loopers for all buffers.

METHOD:: resume
Resume a paused player. Internally this will resume the Ndef that keeps the loopers for all buffers.

METHOD:: clear
Clear the player. This will clear the loopers emphasis::and:: all effects that have been chained to the output of the player. Once an SNSamplePlayer has been cleared it can not be restarted.

ARGUMENT:: fadeTime
A link::Classes/Float:: or link::Classes/Integer::, determining the fade-out time of the player in seconds.

METHOD:: freeHangingNodes
When using mode code::\grain:: (the default) it may happen under certain circumstances that grains are not freed properly (they remain on the server, consuming memory). In these cases calling this method will clear all nodes and free memory. Though this may have an audible effect new grains should build up automatically and it will often be preferable to have a short moment of silence than running out of memory.

METHOD:: setLoopMaxLength
SNSamplePlayer has originally been designed to be used in combination with link::Classes/SNSampler::, allowing you to record sound sequences up to a specified length on the fly. However, sound sequences won't necessarily fill up a whole buffer which means, when looping over the buffer, silence at the end of the buffer will be part of the loop. This method tells the looper how much of the buffer should be used for looping.

The method especially makes sense in combination with the link::Classes/SNSampler#-doneAction#doneAction:: of an link::Classes/SNSampler::. The code::doneAction:: can be passed a numerical code::index:: (the index of the buffer in the array of consecutive buffers for which the maximum loop length shall be set) and the code::length:: of the loop in seconds.

code::
// ~sampler is an existing instance of an SNSampler
// ~looper is an existing instrance of an SNSamplePlayer
~sampler.doneAction_({ |index, length|
	~looper.setLoopMaxLength(index, length);
});
::

ARGUMENT:: index
An link::Classes/Integer:: or a whole-numbered link::Classes/Float::, denoting the index of the buffer in the array of consecutive buffers.

ARGUMENT:: length
A link::Classes/Float::, denoting the length of the loop in seconds.

SUBSECTION:: Instance variables

METHOD:: debug
If set to code::true:: the player will post information on the just played loops to the console.

ARGUMENT:: bool
A link::Classes/Boolean:: (either code::true:: or code::false::)

METHOD:: numOutChannels
The number of output channels. This is not the same as the number of buffers. Internally each buffers plays to its own channel(s). All buffer channels will then be spread over the panorama either using link::Classes/Splay:: (stereo) or link::Classes/SplayAz:: (code::numOutChannels:: > 2).

returns:: An link::Classes/Array:: of link::Classes/Integer::s

METHOD:: loopLengths
As eplained in link::#-setLoopMaxLength:: a buffer may be larger than the actual audio that it contains, leaving silence at the end of the buffer. This variable contains the lengths of actually recorded audio in seconds for all buffers as set by link::#-setLoopMaxLength::.

returns:: An link::Classes/Array:: of link::Classes/Float::

METHOD:: name
The name of the instance as assigned in link::#*new:: (internally converted to a link::Classes/Symbol:: if given as link::Classes/String::).

returns:: A link::Classes/Symbol::

METHOD:: bufLength
The full length of the buffers as given in link::#*new::. This will not be the same as actual length of audio written to them (see link::#-loopLengths::).

returns:: A link::Classes/Integer:: or link::Classes/Float::

METHOD:: server
The link::Classes/Server:: on which to play the audio - either explicitely given in link::#*new:: or simply code::Server.default::.

METHOD:: looperPlayer
The link::Classes/Ndef:: that holds the looper as its source. The looper can basically be a link::Classes/Function:: or a link::Classes/Pattern::. If desired the output can be further processed by hooking up link::Reference/NodeProxy_roles::.

returns:: An link::Classes/Ndef::

METHOD:: out
The link::Classes/Ndef:: that's actually playing to the out channels. The number of channels will be determined by the code::numOutChannels:: argument in link::#*new::. If desired the output can be further processed by hooking up link::Reference/NodeProxy_roles::.

returns:: An link::Classes/Ndef::

METHOD:: group
The link::Classes/Group::, respectively link::Classes/ParGroup:: in which the Ndef returned by link::#*out:: will play. If link::Classes/Server#*scsynth#scsynth:: is set engine this will resolve to link::Classes/Group::. If link::Classes/Server#*supernova#supernova:: is used (multicore virtualistaion) a link::Classes/ParGroup:: will be returned. Setting the Ndef to explicit a group allows you to free all contained nodes contained in the group without stopping the player. See link::#-freeHangingNodes:: for further explanation.

returns:: A link::Classes/Group:: or a link::Classes/ParGroup::

METHOD:: buffers
Get or set the array of consecutive buffers to be looped over.

returns:: An link::Classes/Array:: of link::Classes/Buffer::s

METHOD:: mode
The mode to which the SNSamplePlayer has been initialized: code::\grain::, code::\mono:: or code::\ndef::.

returns:: A link::Classes/Symbol::

PRIVATE::init, prSetUpControls, prSetSpecConstraints, prInitPatternPlayer


EXAMPLES::

code::
// initialize a sampler
// name = 'Sampler', 5 buffers, buffer length = 60 seconds
~sampler = SNSampler.new;

// setting up the sampler will automatically allocate the array
// of consecutive buffers andgenerate controls within CVCenter
~sampler.setupSampler;

// initialize a player
// name = 'Looper', buffer length = 60 seconds, mode = 'grain'
~player = SNSamplePlayer.new;

// set up the player, pass in the buffers to be used
~player.setupPlayer(~sampler.buffers);

// introspection
~player.name; // "Looper"
~player.numOutChannels; // 2
~player.looperPlayer; // the looper, playing out each buffer on a separate channel
~player.looperPlayer.numChannels; // 5
~player.out; // the actual output - all buffer channels mixed down to the specified number of outs
~player.out.numChannels; // 2 - the same as ~player.numOutChannels

// set a done action, to be executed sampling a sequence is done
(
~sampler.doneAction_({ |index, length|
	~player.setLoopMaxLength(index, length);
	~player.resume.play;
})
)

// hook a filter chain
// filters can be hooked up to ~player.looperPlayer - filtering each buffer separately
// or to ~player.out - filter output channels
// control each channel separately
(
var n = ~player.numOutChannels;
Spec.add(\pRatio, #[0.01, 4]);
Spec.add(\pDisp, #[0, 4]);
Spec.add(\pTimeDisp, #[0, 0.2]);
~player.out[1] = \filter -> { |in|
	PitchShift.ar(
		in,
		pitchRatio: \pRatio.kr(1.0!n),
		pitchDispersion: \pDisp.kr(0!n),
		timeDispersion: \pTimeDisp.kr(0!n)
	)
}
)

// control filters through CVCenter - separate control for each channel
~player.out.cvcGui(excemptArgs: #[in]);

// hook up more filters...
// take care of the index of your filters
// we started at 1, next one should at least be 2
// have a look at NodeProxy rules help

// fade out and finish
~player.clear(10);
::
